class Triangulin : Actor {

    const timeToAttack = 175; // 5 seconds in tics

    double timeLeftToAttack;

    OnatGamePlayer pmo;

    bool chaseSoundPlayed;

    Default {
        Monster;
        Obituary "";
        Health 100;
        Radius 20;
        Height 20;
        Speed 20;
    }
    States {
    Spawn:
		TRIN A 1 A_Look;
        TRIN A 0 A_Chase(null, null, CHF_NORANDOMTURN);
		Loop;
    }

    override void BeginPlay(){
        Super.BeginPlay();

        chaseSoundPlayed = false;

        timeLeftToAttack = timeToAttack;

        pmo = OnatGamePlayer(players[0].mo);

        A_StartSound("monsters/triangulin/spawn");

        Deactivate(self);
    }

    override void Tick(){
        Super.Tick();
        if(timeLeftToAttack > 0){
            timeLeftToAttack -= 1;
        } else {
            Activate(self);
            self.goal = pmo;
            self.target = pmo;
            if(!chaseSoundPlayed){
                A_StartSound("monsters/triangulin/chase");
                chaseSoundPlayed = true;
            }
        }


    }

    override void CollidedWith(Actor other, bool passive){
        Super.CollidedWith(other, passive);

        if(other.player){
            A_CustomMeleeAttack(100);
            A_StartSound("weapon/stick/hit");
        }

    }
}
