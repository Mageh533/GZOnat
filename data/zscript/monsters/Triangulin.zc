class Triangulin : Actor {

    const timeToAttack = 175; // 5 seconds in tics
    const flashlightTime = 70;

    double timeLeftToAttack;
    double timeLeftFlashlight;

    OnatGamePlayer pmo;

    bool chaseSoundPlayed;
    bool hitSoundPlayed;

    Default {
        Monster;
        Obituary "";
        Health 100;
        Radius 20;
        Height 20;
        Speed 20;
    }
    States {
    Spawn:
		TRIN A 1 A_Look;
        TRIN A 0 A_Chase(null, null, CHF_NORANDOMTURN);
		Loop;
    }

    override void BeginPlay(){
        Super.BeginPlay();

        chaseSoundPlayed = false;
        hitSoundPlayed = false;
        timeLeftToAttack = timeToAttack;
        timeLeftFlashlight = flashlightTime;

        pmo = OnatGamePlayer(players[0].mo);

        A_StartSound("monsters/triangulin/spawn");

        Deactivate(self);
    }

    override void Tick(){
        Super.Tick();
        if(timeLeftToAttack > 0){
            timeLeftToAttack -= 1;
        } else {
            Activate(self);
            self.goal = pmo;
            self.target = pmo;
            if(!chaseSoundPlayed){
                A_StartSound("monsters/triangulin/chase");
                chaseSoundPlayed = true;
            }
        }

        // bool to check if the player is looking at the monster
        bool playerLookingAt = (((pmo.angle % 360) - (pmo.AngleTo(self) % 360)) < 15 && ((pmo.angle % 360) - (pmo.AngleTo(self) % 360)) > -15);

        // Check that they have a flashlight, are close, the flashlight is turned on and are looking at the monster
        if(pmo.FindInventory("Flashlight")){
            if(Distance2D(pmo) < 500 && Flashlight(pmo.FindInventory("Flashlight")).beam != null && playerLookingAt){
                timeLeftFlashlight -= 1;
            }
        }

        if(timeLeftFlashlight <= 0){
            A_FadeOut(0.0);
        }


    }

    override void CollidedWith(Actor other, bool passive){
        Super.CollidedWith(other, passive);

        if(other.player && timeLeftToAttack <= 0){
            A_CustomMeleeAttack(100);

            if(!hitSoundPlayed){
                A_StartSound("weapon/stick/hit");
                hitSoundPlayed = true;
            }
        }

    }
}
