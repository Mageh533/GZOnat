class Flashlight : Inventory {

    Spotlight beam;
    OnatGamePlayer pmo;

    const maxBattery = 100;

    double battery;

    Default{
        Inventory.MaxAmount 1;
        Inventory.Amount 1;
        Inventory.PickupMessage "$TXT_PICKUP_FLASHLIGHT";
        Inventory.PickupSound "weapon/flashlight/up";
        Scale 0.1;
    }

    States{
    Spawn:
        FLAS A -1;
        Loop;
    }

    // Once the player picks up the flashlight
    override void AttachToOwner(Actor other){
        Super.AttachToOwner(other);

        pmo = OnatGamePlayer(other);

        battery = maxBattery;

        CreateBeam();
    }

    override void DoEffect(){
        Super.DoEffect();

        if(beam){
            beam.SetOrigin((pmo.pos.xy, pmo.player.viewz - 10), true);
            beam.A_SetAngle(pmo.angle, SPF_INTERPOLATE);
            beam.A_SetPitch(pmo.pitch, SPF_INTERPOLATE);

            // Brightness decreases as battery decreases
            beam.args[0] = 255 * (battery / maxBattery);
            beam.args[1] = 255 * (battery / maxBattery);
            beam.args[2] = 255 * (battery / maxBattery);
        }

        // Reload the battery by using the handcrank
        if(pmo.player.cmd.buttons & BT_RELOAD){

            if(battery < maxBattery){
                battery += 0.5;
            }

            if(!pmo.IsActorPlayingSound(CHAN_WEAPON)){
                pmo.A_StartSound("weapon/flashlight/reload", CHAN_WEAPON, 1, ATTN_NORM);
            }

            if(pmo.Noise < 600){
                pmo.Noise += (pmo.Player.ViewHeight != 20.5) ? 10: 40; // Player can keep reloading after reaching 100% but it simply makes noise
            }
            
        } else {
            pmo.A_StopSound(CHAN_WEAPON);
        }

        if(battery > 0 && beam){
            battery -= 0.1;
        } else if(beam && battery <= 0){
            beam.Destroy();
            pmo.A_StartSound("weapon/flashlight/up", CHAN_BODY, 1, ATTN_NORM);
            beam = null;
        }
    }

    void Toggle(){
        if(battery > 0){
            if(beam){
                beam.Destroy();
                beam = null;
            }else{
                CreateBeam();
            }
            pmo.A_StartSound("weapon/flashlight/up", CHAN_BODY, 1, ATTN_NORM);
        }
    }

    private void CreateBeam(){
        beam = Spotlight(Spawn('Spotlight', (pos.xy, pmo.player.viewz - 10)));
        beam.args[0] = 255; //red color in 0-255 range
        beam.args[1] = 255; //green color in 0-255 range
        beam.args[2] = 255; //blue color in 0-255 range
        beam.args[3] = 320; //length of the spotllight
        beam.SpotInnerAngle = 10; //inner angle (brightest area)
        beam.SpotOuterAngle = 30; //outerangle (dimmer area)
        beam.bATTENUATE = true; //make it attenuated to interact with shaders
    }
}


// Event handler for my custom flashlight keybind, assigned in KEYCONF and menudef.lmp
class FlashLightHandler : EventHandler{

    override void NetworkProcess(ConsoleEvent e){
        if(e.name == "flashlight_toggle"){
            Flashlight flashlight = Flashlight(players[e.player].mo.FindInventory("Flashlight"));
            if(flashlight){
                flashlight.toggle();
            }
        }
    }
}
